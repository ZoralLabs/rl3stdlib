#   Copyright 2018 Zoral Limited
#
#   Licensed under the Apache License, Version 2.0 (the "License");
#   you may not use this file except in compliance with the License.
#   You may obtain a copy of the License at
#
#       http://www.apache.org/licenses/LICENSE-2.0
#
#   Unless required by applicable law or agreed to in writing, software
#   distributed under the License is distributed on an "AS IS" BASIS,
#   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#   See the License for the specific language governing permissions and
#   limitations under the License.

include <domain.rl3>

pattern EMAIL_Wrapper(x)
    (
         \s?\{\s?{x}\s?\}\s?
        |\s?\[\s?{x}\s?\]\s?
        |\s?<\s?{x}\s?>\s?
        |\s?\(\s?{x}\s?\)\s?
        |\s?\(\s?\(\s?{x}\s?\)\s?\)\s?
        |\s?\(\s?<\s?{x}\s?>\s?\)\s?
        |\s-{x}-\s
    )

pattern EMAIL_At [icase]
    (
         \s?@\s?
        |\*@\*
        |{EMAIL_Wrapper (@|a|ä|at|a\st|ät|ä\st|att|ätt|miukumauku|se\skumma\skiemura|ätmerkki|_at_|_ät_|miuku|ät-merkki)}
    )

pattern EMAIL_Dot [icase]
    (
         \s?\.\s?
        |\(\.\)
        |{EMAIL_Wrapper (dot|punkt|punct|piste)}
    )

pattern EMAIL_Name [icase]
    ({token}(([+_\-]|{EMAIL_Dot}){token}){0,5})

pattern EMAIL_DomainLabel [icase]
    ({token}((-|{EMAIL_Dot}){token}){0,5})

pattern EMAIL [icase]
    ({EMAIL_Name}{EMAIL_At}{EMAIL_DomainLabel}{EMAIL_Dot}{TLD})
